resource "aws_s3_bucket" "this" {
  bucket = local.bucket_name

  tags = {
    Name        = local.bucket_name
    Environment = "dev"
    Project     = "mmdc-wst"
  }
}

resource "aws_s3_bucket_public_access_block" "this" {
  bucket = aws_s3_bucket.this.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_s3_bucket_versioning" "this" {
  bucket = aws_s3_bucket.this.id

  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "this" {
  bucket = aws_s3_bucket.this.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

locals {
  # Always include the app role. Merge in any extra principals (e.g. local dev IAM users).
  bucket_principals = distinct(
    concat(
      [aws_iam_role.app_role.arn],
      var.allowed_principals,
      var.create_railway_user ? [aws_iam_user.railway[0].arn] : [],
    )
  )

  bucket_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid = "AllowListBucket"
        Effect = "Allow"
        Principal = { AWS = local.bucket_principals }
        Action = ["s3:ListBucket"]
        Resource = aws_s3_bucket.this.arn
      },
      {
        Sid = "AllowObjectActions"
        Effect = "Allow"
        Principal = { AWS = local.bucket_principals }
        Action = ["s3:GetObject", "s3:PutObject", "s3:DeleteObject"]
        Resource = "${aws_s3_bucket.this.arn}/*"
      }
    ]
  })
}

resource "aws_s3_bucket_policy" "this" {
  bucket = aws_s3_bucket.this.id
  policy = local.bucket_policy
  depends_on = [aws_iam_role.app_role]
}
